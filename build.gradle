description = "Spock Framework support for Grails"

apply plugin: "idea"

allprojects { project ->
  version = '0.6-SNAPSHOT'
  group = "org.spockframework"
  isSnapshot = version.endsWith("-SNAPSHOT")
  isCiBuild = project.hasProperty("isCiBuild")

  spockVersion = "0.6-groovy-1.8-SNAPSHOT"
  spockDependency = "org.spockframework:spock-core:$spockVersion"

  grailsVersion = "2.0.0"

  groovyVersion = "1.8.4"
  groovyDependency = "org.codehaus.groovy:groovy-all:$groovyVersion"

  configurations.all {
    if (isCiBuild) {
      resolutionStrategy.cacheChangingModulesFor 10, "minutes"
    } else {
      resolutionStrategy.cacheChangingModulesFor 24, "hours"
    }
  }

  repositories {
    maven { url "http://m2repo.spockframework.org/snapshots" }
    maven { url "http://repo.grails.org/grails/core" }
  }
}

idea {
  project {
    jdkName "1.6"
    ipr {
      withXml { provider ->
        def node = provider.asNode()
        node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
        node.append(new XmlParser().parse(file("ideaCodeStyle.xml")))
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion "1.0-milestone-7"
}

task release(dependsOn: [":spock-grails-support:uploadArchives", ":spock-grails:release"])