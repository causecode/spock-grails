description = "Spock Framework support for Grails"

apply plugin: "idea"

allprojects { project ->
  version = '0.6'
  group = "org.spockframework"
  isSnapshot = true
  isCiBuild = project.hasProperty("ciBuild")

  spockVersion = { groovyVersion ->
    "$version-groovy-$groovyVersion" + (isSnapshot ? "-SNAPSHOT" : "")
  }

  if (isCiBuild) {
    configurations.all { resolutionStrategy.cacheChangingModulesFor 1, "minutes" }
  }

  repositories {
    maven { url "http://repo.grails.org/grails/core" }
  }
}

idea {
  project {
    jdkName "1.6"
    ipr {
      withXml { provider ->
        def node = provider.asNode()
        node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
        node.append(new XmlParser().parse(file("ideaCodeStyle.xml")))
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = "1.0-milestone-8a-20120216221836+0100"
}

task release {
  dependsOn ":spock-grails-support:uploadArchives", ":spock-grails:releasePlugin"
}